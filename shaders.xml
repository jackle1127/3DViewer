<shaders>
    <vertex>
        precision mediump float;
        attribute vec3 aPosition;
        attribute vec3 aNormal;
        attribute vec3 aTangent;
        attribute vec2 aUV;

        uniform mat4 uNormalMatrix;
        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;

        varying vec3 vPosition;
        varying vec2 vUV;
        varying vec3 vNormal;
        varying vec3 vTangent;
        varying vec3 vBitangent;

        void main(void) {
            vPosition = vec3(uMVMatrix * vec4(aPosition, 1.0));
            vNormal = normalize(mat3(uNormalMatrix) * aNormal);
            vTangent = normalize(mat3(uNormalMatrix) * aTangent);
            vBitangent = normalize(cross(vTangent, vNormal));
            vTangent = normalize(cross(vNormal, vBitangent)); // To make sure it's othogonal to nrm.
            vUV = aUV;
            gl_Position = uPMatrix * vec4(vPosition, 1.0);
        }
    </vertex>
    <fragment>
        #define PI 3.1415926535
        #define MIPS_COUNT 9.0
        #define FRESNEL_ROUGHNESS_POWER 8.0
        precision mediump float;

        uniform vec4 uDiffuse;
        uniform vec4 uAmbient;
        uniform vec4 uSpecular;
        uniform float uGloss;
        uniform vec4 uEmission;
        uniform float uNormalMultiplier;
        uniform float uDissolve;
        
        // View to world matrix to calculate reflection.
        uniform mat4 uVMMatrix;
        uniform vec4 uEnvironmentAmbient;
        uniform vec3 uLightDirections[NUM_OF_LIGHTS];
        uniform vec4 uLightColors[NUM_OF_LIGHTS];
        uniform samplerCube uSkybox;
        
        uniform sampler2D uDiffuse2D;
        uniform sampler2D uOcclusion2D;
        uniform sampler2D uNormal2D;
        uniform sampler2D uSpecular2D;
        uniform sampler2D uGloss2D;
        uniform sampler2D uEmission2D;

        varying vec3 vPosition;
        varying vec2 vUV;
        varying vec3 vNormal;
        varying vec3 vTangent;
        varying vec3 vBitangent;

        float clampDot(vec3 v1, vec3 v2) {
            return min(max(dot(v1, v2), 0.0), 1.0);
        }
        
        // Argument VECTOR is up to the calling function.
        float fresnelSchlick(vec3 vector, vec3 viewVector) {
            return pow(1.0 - clampDot(vector, viewVector), 5.0);
        }

        float distributionGGX(vec3 halfway, vec3 normal,
                float roughness) {
            float nDotH2 = clampDot(normal, halfway);
            nDotH2 = nDotH2 * nDotH2;
            roughness = roughness * roughness; // roughness = roughness ^ 2 now.
            float denominator = nDotH2 * (roughness - 1.0) + 1.0;
            return roughness / (PI * denominator * denominator);
        }
        
        // Argument VECTOR will be changed because Smith uses the same equation for 2 different vectors.
        float geometricSmithGGX(vec3 vector, vec3 normal,
                float roughness) {
            float nDotV = clampDot(normal, vector);
            return 2.0 * nDotV / (nDotV + sqrt(roughness + (1.0 - roughness) * nDotV));
        }

        float geometricSmith(vec3 normal, vec3 lightDir,
                vec3 viewVector, float roughness) {
            roughness = roughness * roughness;
            return geometricSmithGGX(lightDir, normal, roughness) * 
                    geometricSmithGGX(viewVector, normal, roughness);
        }

        // We use the common Cook-Torrance BRDF for this.
        vec4 brdfCookTorrance(vec3 normal, vec3 lightDir,
                vec3 viewVector, vec4 lightColor, 
                vec4 specColor, float roughness) {
            vec3 halfway = normalize(lightDir + viewVector);
            float nDotL = clampDot(normal, lightDir) + .0001;
            float nDotV = clampDot(normal, viewVector) + .0001;
            //nDotL = nDotV = 1.0;
            // Fresnel component.
            float fComponent = fresnelSchlick(halfway, viewVector);
            fComponent = pow(fComponent, 1.0 + roughness * FRESNEL_ROUGHNESS_POWER);

            roughness = roughness * roughness;

            // Distribution component.
            float dComponent = distributionGGX(halfway, normal, roughness);
            // Geometric component.
            float gComponent = geometricSmith(normal, lightDir,
                    viewVector, roughness);
            
            //fComponent = pow(fComponent, roughness * FRESNEL_ROUGHNESS_POWER);
            // Modifying the surface specular color instead of putting it in the Fresnel component.
            specColor += (1.0 - specColor) * fComponent;
            
            return lightColor * specColor * gComponent * dComponent
                    / (4.0 * nDotL * nDotV);
        }
        
        // We use the common Cook-Torrance BRDF for this.
        vec4 brdfCookTorranceOptimized(vec3 normal, vec3 lightDir,
                vec3 viewVector, vec4 lightColor, 
                vec4 specColor, float roughness) {
            vec3 halfway = normalize(lightDir + viewVector);
            float nDotL = clampDot(normal, lightDir) + .0001;
            float nDotV = clampDot(normal, viewVector) + .0001;
            
            float fComponent = fresnelSchlick(halfway, viewVector);
            fComponent = pow(fComponent, 1.0 + roughness * FRESNEL_ROUGHNESS_POWER);
            
            roughness = roughness * roughness;
            specColor += (1.0 - specColor) * fComponent;
            
            roughness = roughness * roughness; // roughness = roughness ^ 2 now.
            float nDotH2 = clampDot(normal, halfway);
            nDotH2 = nDotH2 * nDotH2;
            float denominator = nDotH2 * (roughness - 1.0) + 1.0;
            
            return lightColor * specColor * roughness / 
                    (PI * denominator * denominator * 
                    (nDotV + sqrt(roughness + (1.0 - roughness) * nDotV)) *
                    (nDotL + sqrt(roughness + (1.0 - roughness) * nDotL)));
        }
        
        // To fall back to while working on other brdfs.
        vec4 brdfBlinnPhong(vec3 normal, vec3 lightDir,
                vec3 viewVector, vec4 lightColor, 
                vec4 specColor, float roughness) {
            roughness = roughness * roughness * roughness * roughness;
            vec3 halfway = normalize(lightDir + viewVector);
            // Fresnel component.
            float fresnel = fresnelSchlick(halfway, viewVector);
            
            //float amount = pow(clampDot(halfway, normal), 500.0 / (1.001 - roughness));
            // Normalized Blinn-Phong.
            float amount = pow(clampDot(halfway, normal), 2.0 / roughness - 2.0) / (PI * roughness);
            
            // At fresnel = 1, surface reflects 100%.
            specColor = mix (specColor, vec4(1.0), fresnel);
            
            return lightColor * specColor * amount;
        }
        
        void main(void) {
            vec3 viewVector = normalize(vPosition);

            vec4 surfaceDiffuse = uDiffuse * texture2D(uDiffuse2D, vUV);
            vec4 surfaceAO = uAmbient * texture2D(uOcclusion2D, vUV);
            vec4 surfaceNormal = texture2D(uNormal2D, vUV);
            vec4 surfaceSpecColor = uSpecular * texture2D(uSpecular2D, vUV);
            // 1.001 to prevent roughness being exactly 0.
            float surfaceRoughness = 1.001 - uGloss * texture2D(uGloss2D, vUV).r;
            vec4 emission = uEmission * texture2D(uEmission2D, vUV);
            // Calculate surfaceNormal based on bump map.
            vec3 finalNormal = normalize(uNormalMultiplier 
                    * ((surfaceNormal.r - .5) * vBitangent 
                    + (surfaceNormal.g - .5) * vTangent)
                    + (surfaceNormal.b - .5) * vNormal);
            surfaceDiffuse *= 1.0 - surfaceSpecColor;
            
            // This will acumulate through each light source.
            vec4 finalColor = vec4(vec3(emission), 1.0);
            
            // Ambient.
            
            float environmentFresnel = fresnelSchlick(finalNormal, -viewVector);
            environmentFresnel = pow(environmentFresnel, 1.0 + surfaceRoughness * FRESNEL_ROUGHNESS_POWER);
            // At fresnel = 1, surface reflects 100%.
            vec4 environmentSpecColor = mix (surfaceSpecColor, vec4(1.0), environmentFresnel);

            vec3 reflectionVector = reflect(viewVector, finalNormal);
            reflectionVector = mat3(uVMMatrix) * reflectionVector, 0.0;
            
            vec4 specularEnvironment = uEnvironmentAmbient * environmentSpecColor
                    * textureCube(uSkybox, reflectionVector * vec3(1.0, 1.0, -1.0),
                    MIPS_COUNT * mix(surfaceRoughness, 0.0, environmentFresnel) * 6.0);
            
            vec4 diffuseEnvironment = 
                    uEnvironmentAmbient 
                    * textureCube(uSkybox, mat3(uVMMatrix) 
                    * finalNormal * vec3(1.0, 1.0, -1.0), MIPS_COUNT);
            diffuseEnvironment *= surfaceDiffuse * surfaceAO * (1.0 - environmentFresnel);
            
            finalColor += diffuseEnvironment + specularEnvironment;
            
            // Light sources.
            // &lt; because of xml....
            for (int i = 0; i &lt; NUM_OF_LIGHTS; i++) {
                if (dot(vNormal, uLightDirections[i]) &lt; 0.0) {
                    vec4 diffuseReflection = surfaceDiffuse *
                            clampDot(-uLightDirections[i], finalNormal) * uLightColors[i];
                    vec4 specularReflection = brdfCookTorranceOptimized(finalNormal, -uLightDirections[i],
                            -viewVector, uLightColors[i], surfaceSpecColor, surfaceRoughness);
                    finalColor += diffuseReflection + specularReflection;
                }
            }
            
            gl_FragColor = vec4(finalColor.rgb, mix(surfaceDiffuse.a, 1.0, uDissolve));
        }
    </fragment>
    <vertexcube>
        attribute vec2 aProjectedPos;
        
        // ProjectedPos / divisor = clip space.
        uniform vec2 uDivisor;
        
        varying vec3 aViewVector;

        void main(void) {
            aViewVector = vec3(aProjectedPos, -1.0);
            gl_Position = vec4(aProjectedPos / uDivisor, 1.0, 1.0);
        }
    </vertexcube>
    <fragmentcube>
        precision mediump float;
        uniform float uBackgroundLOD;
        uniform samplerCube uSkybox;
        uniform mat4 uVMMatrix;
        uniform float uBackgroundIntensity;

        varying vec3 aViewVector;

        void main(void) {
            gl_FragColor = vec4(vec3(uBackgroundIntensity 
                    * textureCube(uSkybox, vec3(uVMMatrix * vec4(aViewVector, 0))
                    * vec3(1.0, 1.0, -1.0), uBackgroundLOD)), 1.0);
        }
    </fragmentcube>
    <verteximage>
        attribute vec2 aVertexPos;
        
        varying vec2 vUV;

        void main(void) {
            vUV = (aVertexPos + 1.0) / 2.0;
            gl_Position = vec4(aVertexPos, 1.0, 1.0);
        }
    </verteximage>
    <fragmentthreshold>
        // Use the verteximage vertex shader to rasterize.
        
        precision mediump float;
        uniform sampler2D uFrameBuffer;
        uniform float uThreshold;
        uniform float uSoftRange;
        
        varying vec2 vUV;
        
        void main(void) {
            vec4 pixel = texture2D(uFrameBuffer, vUV);
            float weight = 0.2126 * pixel.r + 0.7152 * pixel.g + 0.0722 * pixel.b;
            weight = clamp((weight - (uThreshold - uSoftRange / 2.0)) / uSoftRange
                    , 0.0, 1.0);
            gl_FragColor = vec4((weight * pixel).rgb, 1.0);;
        }
    </fragmentthreshold>
    <fragmentblurhorizontal>
        // Blur horizontally.
        // Use the verteximage vertex shader to rasterize.
        
        #define HALF_WINDOW_SIZE 5.0
        precision mediump float;
        
        uniform sampler2D uFrameBuffer;
        uniform float uTexelSize;
        
        varying vec2 vUV;
        
        void main(void) {
            vec4 pixel = texture2D(uFrameBuffer, vUV);
            for (float i = 1.0; i &lt;= HALF_WINDOW_SIZE; i++) {
                pixel += texture2D(uFrameBuffer, vUV + vec2(uTexelSize * i, 0));
                pixel += texture2D(uFrameBuffer, vUV + vec2(-uTexelSize * i, 0));
            }
            pixel /= HALF_WINDOW_SIZE * 2.0 + 1.0;
            gl_FragColor = vec4(pixel.rgb, 1.0);
        }
    </fragmentblurhorizontal>
    <fragmentblurvertical>
        // Blur vertically.
        // Use the verteximage vertex shader to rasterize.
        
        #define HALF_WINDOW_SIZE 5.0
        precision mediump float;

        uniform sampler2D uFrameBuffer;
        uniform float uTexelSize;
        
        varying vec2 vUV;
        
        void main(void) {
            vec4 pixel = texture2D(uFrameBuffer, vUV);
            for (float i = 1.0; i &lt;= HALF_WINDOW_SIZE; i++) {
                pixel += texture2D(uFrameBuffer, vUV + vec2(0, uTexelSize * i));
                pixel += texture2D(uFrameBuffer, vUV + vec2(0, -uTexelSize * i));
            }
            pixel /= HALF_WINDOW_SIZE * 2.0 + 1.0;
            gl_FragColor = vec4(pixel.rgb, 1.0);
        }
    </fragmentblurvertical>
    <fragmentadd>
        // Use the verteximage vertex shader to rasterize.
        precision mediump float;
        
        uniform sampler2D uFrame1;
        uniform sampler2D uFrame2;
        uniform float uFrame2Strength;
        
        varying vec2 vUV;
        
        void main(void) {
            vec4 pixel = texture2D(uFrame1, vUV);
            pixel += texture2D(uFrame2, vUV) * uFrame2Strength;
            gl_FragColor = vec4(pixel.rgb, 1.0);
        }
    </fragmentadd>
</shaders>